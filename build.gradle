buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'


group = GROUP
version = VERSION_NAME

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 30
        multiDexEnabled true
        testApplicationId "com.mixpanel.android.mpmetrics"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        consumerProguardFiles 'proguard.txt'

        buildConfigField "String", "MIXPANEL_VERSION", "\"${version}\""

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources"
            textOutput "stdout"
            textReport true
        }

        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }
    buildTypes {
        debug{
            minifyEnabled false
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    repositories {
        mavenCentral()
        google()
    }
}

// Note- *all* dependencies are marked as optional in our final
// pom! If you need a transitive dependency in the library, you'll
// have to change the bit in uploadArchives that marks all dependencies as optional.
dependencies {
    api 'com.android.installreferrer:installreferrer:2.2'
    implementation "androidx.annotation:annotation:1.1.0"
    
    // AndroidJUnitRunner and JUnit Rules
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:truth:1.4.0'
    androidTestImplementation "org.mockito:mockito-android:2.25.1"
    androidTestUtil 'androidx.test:orchestrator:1.4.0'
    testImplementation "org.mockito:mockito-core:2.25.1"
    
}

apply from: rootProject.file('maven.gradle')
