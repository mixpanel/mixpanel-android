buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:9.0.0'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:2.2.10'
        classpath 'org.jetbrains.kotlin:compose-compiler-gradle-plugin:2.2.10'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'jacoco'


group = GROUP
version = VERSION_NAME

android {
    namespace "com.mixpanel.android"
    compileSdk 34
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }


    defaultConfig {
        minSdk 21
        multiDexEnabled true
        android.buildFeatures.buildConfig true
        testApplicationId "com.mixpanel.android.mpmetrics"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard.txt'

        buildConfigField "String", "MIXPANEL_VERSION", "\"${version}\""

        lintOptions {
            abortOnError true
            warningsAsErrors false
            disable "UnusedResources"
            textOutput "stdout"
            textReport true
        }

        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'
        }
    }
    buildTypes {
        debug{
            minifyEnabled false
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled false
        }
    }
    lint {
        abortOnError false
        targetSdk 34
    }
    testOptions {
        targetSdk 34
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
                jvmArgs '-Xmx1g'
                // Required for JaCoCo to work with Robolectric
                systemProperty 'jacoco-agent.destfile', "${buildDir}/jacoco/testDebugUnitTest.exec"
                jacoco {
                    includeNoLocationClasses = true
                    excludes = ['jdk.internal.*']
                }
            }
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
    repositories {
        mavenCentral()
        google()
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


dependencies {
    implementation "androidx.annotation:annotation:1.8.2"
    implementation 'androidx.core:core:1.13.1'
    // AndroidJUnitRunner and JUnit Rules
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:core:1.6.1'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test:rules:1.6.1'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.ext:truth:1.6.0'
    androidTestImplementation "org.mockito:mockito-android:2.25.1"
    androidTestUtil 'androidx.test:orchestrator:1.5.0'
    testImplementation "org.mockito:mockito-core:5.8.0"
    testImplementation 'org.robolectric:robolectric:4.11.1'
    testImplementation 'androidx.test:core:1.5.0'
    testImplementation 'org.json:json:20231013'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testDebugUnitTest'

    reports {
        xml.required = true
        html.required = true
        xml.outputLocation = file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
        html.outputLocation = file("${buildDir}/reports/jacoco/test/html")
    }

    def fileFilter = [
        '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
        // Exclude classes that require Android framework (emulator) for testing
        // These are covered by instrumented tests instead
        '**/MixpanelAPI.class', '**/MixpanelAPI$*.class',
        '**/AnalyticsMessages.class', '**/AnalyticsMessages$*.class',
        '**/MPDbAdapter.class', '**/MPDbAdapter$*.class',
        '**/PersistentIdentity.class', '**/PersistentIdentity$*.class',
        '**/HttpService.class', '**/HttpService$*.class',
        '**/SystemInformation.class', '**/SystemInformation$*.class',
        '**/MixpanelActivityLifecycleCallbacks.class', '**/MixpanelActivityLifecycleCallbacks$*.class',
        '**/ResourceReader.class', '**/ResourceReader$*.class',
        '**/SharedPreferencesLoader.class', '**/SharedPreferencesLoader$*.class',
        '**/ExceptionHandler.class', '**/ExceptionHandler$*.class',
        '**/ConfigurationChecker.class', '**/ConfigurationChecker$*.class',
        '**/FeatureFlagManager.class', '**/FeatureFlagManager$*.class',
        '**/SessionReplayBroadcastReceiver.class',
        '**/RemoteService.class', '**/RemoteService$*.class',
        '**/ProxyServerInteractor.class',
        '**/MixpanelNetworkErrorListener.class'
    ]
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug/compileDebugJavaWithJavac/classes", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([debugTree]))
    executionData.setFrom(fileTree(dir: "${buildDir}", includes: ['**/*.exec']))
}

apply from: rootProject.file('maven.gradle')
